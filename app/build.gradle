apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "exequiel.ussdwizardhelper"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 5
        versionName "1.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    flavorDimensions "apiUrl"

    productFlavors {
        devel {
            dimension "apiUrl"
            buildConfigField 'String', 'BASE_URL', baseUrl
        }

        preprod {
            dimension "apiUrl"
            buildConfigField 'String', 'BASE_URL', baseUrlPreProd
        }

        prod {
            dimension "apiUrl"
            buildConfigField 'String', 'BASE_URL', baseUrlProd
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'PLANPARAM', planparam
        it.buildConfigField 'String', 'PSW', psw
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

ext {
    supportLibraryVersion = '26.1.0'
    retrofitVersion = '2.3.0'
    butterKnifeVersion = '8.8.1'
    daggerVersion = '2.9'
    okhttpVersion = '3.10.0'
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:26.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    //soap interactions
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation("com.squareup.retrofit2:converter-simplexml:$retrofitVersion") {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }

    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttpVersion"

    //JSON Parsing
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.google.code.gson:gson:2.8.1'

    //injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    //RxJava
    implementation 'io.reactivex:rxandroid:1.2.0'
    implementation 'io.reactivex:rxjava:1.3.0'
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'
}
